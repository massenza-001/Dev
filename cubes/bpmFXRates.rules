#Region System
FEEDSTRINGS;
SKIPCHECK;
#EndRegion

#Region - MISC DEFAULTS NONE and USD/USD_RPT - Force to 1 exchange rate;
#======================================================
# NONE - Rate Type should always be 1
# USD Currency rate alwayus 1 to convert to US
#======================================================
[ 'None' ] = 1 ;
[ 'USD', 'USD_Rpt' ] = N: 1 ;

#Make Total Currencies ZERO since can not consolidate.
[ 'Total Currency' ] = 0;
#EndRegion


#Region - Set Prior Year Rates
[ 'PYAR' ]= N:
     DB('bpmFXRates',!bpmCurrency,!bpmFX_Rpt, 'CYAR',!bpmFX_CurrencyType,
          DB('}ElementAttributes_bpmPeriod',!bpmPeriod,'YoY Period')
     );
[ 'PYBR' ]= N:
     DB('bpmFXRates',!bpmCurrency,!bpmFX_Rpt, 'CYBR',!bpmFX_CurrencyType,
          DB('}ElementAttributes_bpmPeriod',!bpmPeriod,'YoY Period')
     );

#EndRegion

#Region - Set Previous Ending
#======================================================
# Set previous ending element based on period attribute
#======================================================
[ 'Previous Ending' ] = N:
     IF( SUBST( !bpmPeriod, 1, 3 )@='OPB'
     ,
          [ 'Ending' ]
     ,
          DB( 'bpmFXRates', !bpmCurrency, !bpmFX_Rpt, !bpmFX_RateType, 'Ending',
                DB( '}ElementAttributes_bpmPeriod', !bpmPeriod, 'PrevPeriod')
          )
     );
#EndRegion

#Region - Year over Year Change
#======================================================
# Calculate Year over year change using Period Info Attribute
#======================================================
[ 'YoY Ending Change' ]= N:
     ['Ending'] -
     DB('bpmFXRates',!bpmCurrency,!bpmFX_Rpt,!bpmFX_RateType, 'Ending',
          DB('}ElementAttributes_bpmPeriod',!bpmPeriod,'YoY Period')
     );

[ 'YoY Average Change' ]= N:
     ['Average'] -
     DB('bpmFXRates',!bpmCurrency,!bpmFX_Rpt,!bpmFX_RateType, 'Average',
          DB('}ElementAttributes_bpmPeriod',!bpmPeriod,'YoY Period')
     );

#EndRegion

#Region - Derive all other RPT currencies from USD
#======================================================
# Derive all other RPT currencies relative to USD
# Note: add additional reporting currencies as needed to left side
#======================================================
[ {'CAD_Rpt', 'EUR_Rpt'} ] = N:
# If same as RPT Currency set to 1
     IF( !bpmCurrency @= SUBST(!bpmFX_Rpt,1 ,3 )
     ,
          1
     ,
# Otherwise get based on USD
          IF( !bpmCurrency@='USD',
               1 \
               DB( 'bpmFXRates', SUBST( !bpmFX_Rpt, 1, 3), 'USD_Rpt', !bpmFX_RateType, !bpmFX_CurrencyType, !bpmPeriod )
          ,
               DB( 'bpmFXRates', !bpmCurrency, 'USD_Rpt', !bpmFX_RateType,
                    !bpmFX_CurrencyType, !bpmPeriod ) *
               DB( 'bpmFXRates', 'USD', !bpmFX_Rpt, !bpmFX_RateType, !bpmFX_CurrencyType,
                    !bpmPeriod )
          )
     );
#EndRegion

#Region FEEDERS

FEEDERS;
[ 'CYAR' ]=> [ 'PYAR' ];
[ 'CYBR' ]=> [ 'PYBR' ];

['Ending'] => ['Ending Change'], [ 'YoY Ending Change' ] ;
['Average']=> ['YoY Average Change'];

#EndRegion
