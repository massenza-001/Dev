#Region System
FEEDSTRINGS;
SKIPCHECK;
#EndRegion

#Region - Rule over Actual into Plan Scenario
#======================================================
# Actual data, controlled by }bpmPeriod_Info which determines which are plan months
# 0 = Actuals, 1 = Plan
#======================================================
[ 'Plan' ]= N:
     IF( DB( 'bpmPeriod_Info', !bpmPeriod, 'IsPlanMonth' ) <= 0
     ,
          [ 'Actuals' ]
     ,
          Continue
     );
#EndRegion

#Region Revenue Measures
# ======================================================
# Pull data from Price cube
# ======================================================

#['Plan', {'Unit Net Sales Price', 'Unit Direct Cost'}] = N:
[ {'Unit Net Sales Price', 'Unit Direct Cost' } ] = N:
   DB('bpmRevenues_Assumptions', !bpmScenario, !bpmProducts, !bpmChannel, !bpmPeriod, !bpmRevenue_Msr);

[ 'Unit Direct Cost' ] = C:
     [ 'Direct COGS' ] \ [ 'Volume - Units' ];

[ 'Unit Net Sales Price' ] = C:
     [ 'Gross Revenue' ] \ [ 'Volume - Units' ];

#[ 'Plan', 'Gross Revenue' ] = N:
[ 'Plan', 'Gross Revenue' ] = N:
   [ 'Volume - Units' ] *
   [ 'Unit Net Sales Price' ];

#['Plan', 'Direct COGS' ] = N:
[ 'Direct COGS' ] = N:
   [ 'Volume - Units' ] *
   [ 'Unit Direct Cost' ];

#EndRegion

FEEDERS;

# ======================================================
# Current Actual feeds PLAN
# ======================================================
[ 'Actuals', '2015' ] => [ 'Plan' ];
[ 'Actuals', '2016' ] => [ 'Plan' ];

['Plan', 'Volume - Units'] =>
	DB('bpmMetrics_Revenue', 'Quantity Sold' , !bpmProducts, !bpmChannel, !bpmCountry, !bpmPeriod, 'Plan vs Final Budget', 'Actual');

['Predictive', 'Volume - Units'] =>
	DB('bpmMetrics_Revenue', 'Quantity Sold' , !bpmProducts, !bpmChannel, !bpmCountry, !bpmPeriod, 'Plan vs Predictive', 'Target');

[ 'Volume - Units'] =>
     ['Unit Net Sales Price'], ['Unit Direct Cost'], ['Gross Revenue'], ['Direct COGS'];