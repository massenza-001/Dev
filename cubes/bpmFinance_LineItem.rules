#Region System
FEEDSTRINGS;
SKIPCHECK;
#EndRegion

#Region CAPEX Link - Pulls CAPEX MODEL values into line 1 <<NEED TO FIX COMMENT REFERENCE>>
# ============================================================================
# Pulles CAPEX Depreciation for NEW items from CAPEX Model into Depreceation Account
# ============================================================================
[ 'Plan', '609000', 'Line 1', bpmFinance_LineItem_Msr:'Comment' ] = S:
     IF( !bpmPeriod @= DB( '}ElementAttributes_bpmPeriod', !bpmPeriod, 'Current Year' )
     ,
          'Feed from Capex'
     ,
          CONTINUE
     ) ;

[ 'Plan', '609000', 'Line 1' ] = N:
# Only set comment for plan years
     IF( DB( 'bpmPeriod_Info', !bpmPeriod, 'IsPlanMonth' ) <> 0
     ,
# Only bring over CAPEX items that have been FLAGGED as YES for APPROVAL
          DB( 'bpmFinance_Capex', 'Plan', !bpmCompany, !bpmDepartment,
               'Total Asset Types', 'Total Items', 'Total Projects', !bpmCurrency,
               !bpmPeriod, 'Depreciation', 'Yes' )
     ,
          0
     );
#EndRegion

#Region ACCOUNT LEVEL Link
# ============================================================================
# Pull account value from Finance Cube if there are no Line Item value
# ============================================================================
[ 'Plan', 'Account Level Value' ] = N:
# If ACTUAL Month set to value from bpmFinanc
# this is done even when future months are at LID level so LID cube has full bpmFinance values.
     IF( DB( 'bpmPeriod_Info', !bpmPeriod, 'IsPlanMonth' ) = 0
     ,
		DB( 'bpmFinance', !bpmScenario, !bpmCompany, !bpmDepartment, !bpmAccount, !bpmCurrency, !bpmPeriod, !bpmFinance_LineItem_Msr )
	,
	     IF( DB( 'bpmFinance_LineItem', !bpmScenario, !bpmCompany, !bpmDepartment,
     	          !bpmAccount, !bpmCurrency, 'Total Line Items', DB( '}ElementAttributes_bpmPeriod', !bpmPeriod, 'Current Year' ) | 'HasData', !bpmFinance_LineItem_Msr ) <> 0
	     ,
     	     0
	     ,
     	     DB( 'bpmFinance', !bpmScenario, !bpmCompany, !bpmDepartment, !bpmAccount, !bpmCurrency, !bpmPeriod, !bpmFinance_LineItem_Msr )
		)
     );
#EndRegion

#Region - PLAN Control Data Entry
# ======================================================
# CONTROL DATA ENTRY - Only allow Data Entry when a comment has been added.
#  Force ACTUAL Months of the PLAN to 0
# ======================================================
[ 'Plan' ]= N:
# If ACTUAL Month set to zero
     IF( DB( 'bpmPeriod_Info', !bpmPeriod, 'IsPlanMonth' ) = 0
     ,
          0
     ,
          STET
     );
#EndRegion

#Region FEEDERS

FEEDERS;

# ============================================================================
# Feed Line Item Detail to bpmFinance (NOTE: Only link PLAN all other scenarios are NOT live linked)
# ============================================================================
['Plan','Total Line Items']=>
       DB( 'bpmFinance', !bpmScenario, !bpmCompany, !bpmDepartment, !bpmAccount, !bpmCurrency, !bpmPeriod, 'Original' );

#EndRegion