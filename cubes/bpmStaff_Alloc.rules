#Region System
FEEDSTRINGS;
SKIPCHECK;
#EndRegion

#Region ZERO Actuals Periods for PLAN Scenario
#======================================================
# Actual data, controlled by }bpmPeriod_Info which determines which are plan months
# -1 = Prior Year Actuals, 0 = Current Year Actuals, 1 = Plan
#======================================================
['Plan'] = N:
     IF( DB( 'bpmPeriod_Info', !bpmPeriod, 'IsPlanMonth' ) <= 0
     ,
# Actual Period so ZERO
          0
     ,
# Opening Balance so Zero
		IF (SUBST(!bpmPeriod,1,3) @= 'OPB'
		,
			0
		,
# Plan Month so continue to LINK to bpmStaff RULE
			CONTINUE
		)
	);
#EndRegion

#Region LINK to bpmStaff
# =========================================
# If home comp/dept, use calculated %, otherwise use the entered %
# =========================================
['Plan'] = N:
 IF (  (!bpmCompany @= !bpmCompany_Target) &
         (!bpmDepartment @= !bpmDepartment_Target),

# HOME so use CLACULATED %
                DB('bpmStaff',!bpmScenario,!bpmCompany,!bpmDepartment,
                     !bpmStaff_Employee,
                     !bpmPeriod,!bpmStaff_Msr) *

                DB('bpmStaff_Emp_Alloc_Info',
                     !bpmScenario,
                     !bpmCompany,!bpmCompany_Target,
                     !bpmDepartment,!bpmDepartment_Target,
                     !bpmStaff_Employee,'Allocation % - Calculated')
,

# NOT HOME so use ENTERED %
                DB('bpmStaff',!bpmScenario,!bpmCompany,!bpmDepartment,
                     !bpmStaff_Employee, !bpmPeriod,
                     !bpmStaff_Msr) *

                DB('bpmStaff_Emp_Alloc_Info',
                     !bpmScenario,
                     !bpmCompany,!bpmCompany_Target,
                     !bpmDepartment,!bpmDepartment_Target,
                     !bpmStaff_Employee,'Allocation % - Entered')
) ;
#EndRegion

#Region FEEDERS

FEEDERS;
[ 'Plan', 'Employed' ] =>
     ['Gross pay'], ['Bonus'],
     ['FICA'], ['Medicare'], ['State Unemployment'], ['Federal Unemployment'], ['Other Taxes'],
     ['401KMatch'], ['Medical'], ['Dental'], ['Life Insurance'], ['Other Benefits'],
     ['Headcount - Full Time'], ['Headcount - Part Time']
;

[ 'Plan', 'Employed' ] =>
	DB('bpmFinance', !bpmScenario, !bpmCompany_Target, !bpmDepartment_Target, 'Compensation', 'USD', !bpmPeriod, 'Original' );
#EndRegion