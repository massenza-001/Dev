#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#=============================================================================================
# PURPOSE - Process will create and populate two aliases within a dimension.  
# - The first alias will use a format of: Element Code â€“ Description 
# - The second alias will use a format of: Description (Element Code)
#
# PARAMETERS:
#     parDim               - Dimension Name
#     parAlias1            - Alias 1 Name
#     parAlias2            - Alias 2 Name
#=============================================================================================



#=============================================================================================
#>>> Ensure the two aliases exist 
#=============================================================================================

strDimAttr = '}ElementAttributes_' | parDim;

strAttr = parAlias1;
IF( DIMIX( strDimAttr, strAttr ) = 0);
     ATTRINSERT( parDim, '', strAttr, 'A' );
ENDIF;

strAttr = parAlias2;
IF( DIMIX( strDimAttr, strAttr ) = 0);
     ATTRINSERT( parDim, '', strAttr, 'A' );
ENDIF;


#=============================================================================================
#>>> Loop through dimension and populate aliases
#=============================================================================================

intCtrMax = DIMSIZ ( parDim ) ;

intCtr = 1 ;

WHILE ( intCtr <= intCtrMax ) ;
     strElem = DIMNM ( parDim , intCtr ) ;
     strName = ATTRS ( parDim , strElem , 'Name' ) ;
     IF ( strName @<> '' & strName @<> strElem ) ;
          strCodeName = strElem | ' - ' | strName ;
          strNameCode = strName | ' (' | strElem | ')' ;
     ELSE ;
          strCodeName = strElem ;
          strNameCode = strElem ;
     ENDIF ;
     ATTRPUTS ( strCodeName , parDim , strElem , parAlias1 ) ;
     ATTRPUTS ( strNameCode , parDim , strElem , parAlias2 ) ;
     intCtr = intCtr + 1 ;
END ;

#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion